USE PetCareX;
GO

/* --- 1. SEQUENCES --- */
CREATE SEQUENCE dbo.SEQ_KHACHHANG START WITH 1 INCREMENT BY 1;
CREATE SEQUENCE dbo.SEQ_THUCUNG START WITH 1 INCREMENT BY 1;
CREATE SEQUENCE dbo.SEQ_NHANVIEN START WITH 1 INCREMENT BY 1;
CREATE SEQUENCE dbo.SEQ_HOADON START WITH 1 INCREMENT BY 1;
CREATE SEQUENCE dbo.SEQ_PHIENDICHVU START WITH 1 INCREMENT BY 1;
GO

/* --- 2. NHÓM QUẢN LÝ TỔ CHỨC & KHÁCH HÀNG --- */
CREATE TABLE CHINHANH (
    MaCN VARCHAR(10) PRIMARY KEY,
    TenCN NVARCHAR(100) NOT NULL,
    Diachi NVARCHAR(255), SDT VARCHAR(15), 
    Giomocua TIME, Giodongcua TIME,
    CONSTRAINT CHK_CHINHANH_Gio CHECK (Giomocua IS NULL OR Giodongcua IS NULL OR Giomocua <= Giodongcua)
);

CREATE TABLE KHACHHANG (
    MaKH VARCHAR(10) PRIMARY KEY DEFAULT ('KH' + RIGHT('000000' + CAST(NEXT VALUE FOR dbo.SEQ_KHACHHANG AS VARCHAR(6)), 6)),
    Hoten NVARCHAR(100) NOT NULL, CCCD VARCHAR(20) UNIQUE, SDT VARCHAR(15), Email VARCHAR(100),
    Gioitinh NVARCHAR(10) CHECK (Gioitinh IN (N'Nam', N'Nữ')),
    Ngaysinh DATE, 
    Bac NVARCHAR(20) DEFAULT N'Cơ bản' CHECK (Bac IN (N'Cơ bản', N'Thân thiết', N'VIP')),
    Tichluy INT DEFAULT 0 CHECK (Tichluy >= 0)
);

CREATE TABLE TAIKHOAN_MATKHAU (
    MaKH VARCHAR(10) PRIMARY KEY REFERENCES KHACHHANG(MaKH),
    Tendangnhap VARCHAR(50) NOT NULL UNIQUE,
    Matkhau VARCHAR(255) NOT NULL
);

CREATE TABLE THUCUNG (
    MaThuCung VARCHAR(10) PRIMARY KEY DEFAULT ('TC' + RIGHT('000000' + CAST(NEXT VALUE FOR dbo.SEQ_THUCUNG AS VARCHAR(6)), 6)),
    MaKH VARCHAR(10) REFERENCES KHACHHANG(MaKH),
    Ten NVARCHAR(50) NOT NULL, Loai NVARCHAR(50), Giong NVARCHAR(50), NgaySinh DATE,
    GioiTinh NVARCHAR(10) CHECK (GioiTinh IN (N'Đực', N'Cái')), TinhTrangSK NVARCHAR(MAX)
);

/* --- 3. NHÓM NHÂN VIÊN & ĐIỀU ĐỘNG --- */
CREATE TABLE NHANVIEN (
    MaNV VARCHAR(10) PRIMARY KEY DEFAULT ('NV' + RIGHT('000000' + CAST(NEXT VALUE FOR dbo.SEQ_NHANVIEN AS VARCHAR(6)), 6)),
    HoTen NVARCHAR(100) NOT NULL, NgaySinh DATE, GioiTinh NVARCHAR(10) CHECK (GioiTinh IN (N'Nam', N'Nữ')),
    ChucVu NVARCHAR(50) CHECK (ChucVu IN (N'Bác sĩ thú y', N'Quản lí', N'Tiếp tân', N'Nhân viên bán hàng')),
    MaCN VARCHAR(10) REFERENCES CHINHANH(MaCN), Luong DECIMAL(18, 2), Thuong DECIMAL(18, 2)
);

CREATE TABLE LICHSUDIEUDONG (
    MaNV VARCHAR(10) REFERENCES NHANVIEN(MaNV),
    MaCN VARCHAR(10) REFERENCES CHINHANH(MaCN),
    NgayBD DATE NOT NULL, NgayKT DATE NOT NULL, ChucVu NVARCHAR(50),
    PRIMARY KEY (MaNV, MaCN, NgayBD),
    CONSTRAINT CHK_LSDD_Ngay CHECK (NgayBD < NgayKT)
);

/* --- 4. NHÓM SẢN PHẨM, VACCINE & DỊCH VỤ --- */
CREATE TABLE DICHVU (MaDV VARCHAR(10) PRIMARY KEY, TenDV NVARCHAR(100) NOT NULL);

CREATE TABLE CUNGCAPDICHVU (
    MaCN VARCHAR(10) REFERENCES CHINHANH(MaCN),
    MaDV VARCHAR(10) REFERENCES DICHVU(MaDV),
    PRIMARY KEY (MaCN, MaDV)
);

CREATE TABLE SANPHAM (
    MaSP VARCHAR(10) PRIMARY KEY, TenSP NVARCHAR(100) NOT NULL,
    LoaiSP NVARCHAR(50) NOT NULL, -- Thuốc, Đồ ăn, Đồ chơi...
    DonGia DECIMAL(18, 2) CHECK (DonGia > 0)
);

CREATE TABLE VACCINE (
    MaVC VARCHAR(10) PRIMARY KEY, TenVC NVARCHAR(100) NOT NULL, NgaySanXuat DATE,
    LieuLuong FLOAT CHECK (LieuLuong > 0), DonGia DECIMAL(18, 2) CHECK (DonGia > 0)
);

CREATE TABLE GOITIEMPHONG (
    MaGoi VARCHAR(10) PRIMARY KEY, TenGoi NVARCHAR(100) NOT NULL,
    ThoiGian INT CHECK (ThoiGian > 0), -- số tháng hiệu lực
    KhuyenMai FLOAT DEFAULT 0 CHECK (KhuyenMai BETWEEN 0 AND 100)
);

CREATE TABLE GOITIEMPHONG_VACCINE (
    MaGoi VARCHAR(10) REFERENCES GOITIEMPHONG(MaGoi),
    MaVC VARCHAR(10) REFERENCES VACCINE(MaVC),
    SoLieu FLOAT CHECK (SoLieu > 0), PRIMARY KEY (MaGoi, MaVC)
);

/* --- 5. QUẢN LÝ KHO (TỒN KHO THEO CHI NHÁNH) --- */
CREATE TABLE CHINHANH_SANPHAM (
    MaCN VARCHAR(10) REFERENCES CHINHANH(MaCN),
    MaSP VARCHAR(10) REFERENCES SANPHAM(MaSP),
    SoLuongTonKho INT DEFAULT 0 CHECK (SoLuongTonKho >= 0), PRIMARY KEY (MaCN, MaSP)
);

CREATE TABLE CHINHANH_VACCINE (
    MaCN VARCHAR(10) REFERENCES CHINHANH(MaCN),
    MaVC VARCHAR(10) REFERENCES VACCINE(MaVC),
    SoLuongTonKho INT DEFAULT 0 CHECK (SoLuongTonKho >= 0), PRIMARY KEY (MaCN, MaVC)
);

/* --- 6. NHÓM GIAO DỊCH & HÓA ĐƠN --- */
CREATE TABLE HOADON (
    MaHoaDon VARCHAR(10) PRIMARY KEY DEFAULT ('HD' + RIGHT('000000' + CAST(NEXT VALUE FOR dbo.SEQ_HOADON AS VARCHAR(6)), 6)),
    NgayLap DATETIME DEFAULT GETDATE(),
    NhanVienLap VARCHAR(10) REFERENCES NHANVIEN(MaNV),
    MaKH VARCHAR(10) REFERENCES KHACHHANG(MaKH),
    TongTien DECIMAL(18, 2) DEFAULT 0,
    KhuyenMai FLOAT DEFAULT 0 CHECK (KhuyenMai BETWEEN 0 AND 100),
    HinhThucThanhToan NVARCHAR(50) CHECK (HinhThucThanhToan IN (N'Chuyển khoản', N'Tiền mặt'))
);

CREATE TABLE PHIENDICHVU (
    MaPhien VARCHAR(10) PRIMARY KEY 
        DEFAULT ('PD' + RIGHT('000000' + CAST(NEXT VALUE FOR dbo.SEQ_PHIENDICHVU AS VARCHAR(6)), 6)),

    MaHoaDon VARCHAR(10) NULL 
        REFERENCES HOADON(MaHoaDon),

    MaThuCung VARCHAR(10) NULL 
        REFERENCES THUCUNG(MaThuCung),

    MaDV VARCHAR(10) NOT NULL 
        REFERENCES DICHVU(MaDV),

    GiaTien DECIMAL(18, 2) DEFAULT 0 CHECK (GiaTien >= 0),

    TrangThai NVARCHAR(20) NOT NULL DEFAULT N'BOOKING'
        CHECK (TrangThai IN (
            N'BOOKING',      -- đặt lịch / giỏ hàng
            N'IN_SERVICE',   -- đang khám / đang làm
            N'CONFIRMED',    -- đã thanh toán
            N'CANCELLED'
        )),

    ThoiDiemBatDau DATETIME NULL,
    ThoiDiemKetThuc DATETIME NULL
);


CREATE TABLE KHAMBENH (
    MaPhien VARCHAR(10) PRIMARY KEY REFERENCES PHIENDICHVU(MaPhien),
    BacSiPhuTrach VARCHAR(10) REFERENCES NHANVIEN(MaNV),
    CacTrieuChung NVARCHAR(MAX), ChanDoan NVARCHAR(MAX), NgayTaiKham DATE
);

CREATE TABLE TOATHUOC (
    MaPhien VARCHAR(10) REFERENCES PHIENDICHVU(MaPhien),
    MaThuoc VARCHAR(10) REFERENCES SANPHAM(MaSP),
    Soluong INT CHECK (Soluong > 0), PRIMARY KEY (MaPhien, MaThuoc)
);

CREATE TABLE TIEMPHONG (
    MaPhien VARCHAR(10) PRIMARY KEY REFERENCES PHIENDICHVU(MaPhien),
    MaVC VARCHAR(10) REFERENCES VACCINE(MaVC),
    MaGoi VARCHAR(10) REFERENCES GOITIEMPHONG(MaGoi),
    BacSiPhuTrach VARCHAR(10) REFERENCES NHANVIEN(MaNV),
    NgayTiem DATE NOT NULL, SoLieu FLOAT CHECK (SoLieu > 0)
);

CREATE TABLE MUAHANG (
    MaPhien VARCHAR(10) REFERENCES PHIENDICHVU(MaPhien),
    MaSP VARCHAR(10) REFERENCES SANPHAM(MaSP),
    SoLuong INT CHECK (SoLuong > 0), PRIMARY KEY (MaPhien, MaSP)
);

CREATE TABLE MUA_GOI (
    MaKH VARCHAR(10) REFERENCES KHACHHANG(MaKH),
    MaGoi VARCHAR(10) REFERENCES GOITIEMPHONG(MaGoi),
    MaHoaDon VARCHAR(10) REFERENCES HOADON(MaHoaDon),
    PRIMARY KEY (MaKH, MaGoi, MaHoaDon)
);

CREATE TABLE GOI_KHACHHANG_VACCINE (
    MaKH VARCHAR(10) REFERENCES KHACHHANG(MaKH),
    MaGoi VARCHAR(10) REFERENCES GOITIEMPHONG(MaGoi),
    MaVC VARCHAR(10) REFERENCES VACCINE(MaVC),
    Solieuconlai FLOAT DEFAULT 0 CHECK (Solieuconlai >= 0),
    PRIMARY KEY (MaKH, MaGoi, MaVC)
);

CREATE TABLE NHANXET (
    MaKH VARCHAR(10) REFERENCES KHACHHANG(MaKH),
    MaHoaDon VARCHAR(10) REFERENCES HOADON(MaHoaDon),
    DiemDV INT CHECK (DiemDV BETWEEN 1 AND 10),
    Mucdohailong INT CHECK (Mucdohailong BETWEEN 1 AND 5),
    Thaidonhanvien NVARCHAR(MAX), Binhluan NVARCHAR(MAX),
    PRIMARY KEY (MaKH, MaHoaDon)
);
GO

ALTER TABLE DICHVU
ADD DonGia DECIMAL(18,2) NOT NULL
    CONSTRAINT CHK_DICHVU_DonGia CHECK (DonGia >= 0);

INSERT INTO NHANVIEN (MaNV, HoTen, ChucVu)
VALUES ('NV_SYSTEM', N'Hệ thống thanh toán online', N'Nhân viên bán hàng');